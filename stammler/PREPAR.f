CPREPARE      PREPARE/COLPROB, BARILOCHE 1979.
      SUBROUTINE PREPARE(RAD, VOL, SIG, SIGS, SIGR, SIG1, SEC, SIGV)
C
C READING GEOMETRY AND COMPOSITION DATA.
C CONSTRUCTING DERIVED QUANTITIES, LIKE
C   SIGV() = TOTAL XS TIMES VOLUME,
C   SIG1() = THE INVERSE TOTAL XS,
C   SEC()  = NUMBER OF SECONDARIES.
C
      DIMENSION RAD(1), VOL(1), SIG(1), SIGS(1), SIGR(1), SIG1(1),
     1          SEC(1), SIGV(1)
      COMMON / CPR001 / NIN, NOT, NX1(2)
      COMMON / CPR002 / MMAX, IG, NX203, VTOT, SB, NX206
      LOGICAL SWERR, SWHITE
      DATA PI / 3.1415927 /
      DATA SMALL / 1.0E-08 /
C
 1010 FORMAT(6E12.4)
 2010 FORMAT(1H1,25X,25(1H*)/
     1           26X,19H* NUMBER OF REGIONS,I4,2H */
     2           26X,21H* NO. OF GAUSS POINTS,I2,2H */
     3           26X,25(1H*)//)
 2020 FORMAT(55H  REGION   RADIUS      VOLUME      SIGTOT      SIGSCAT ,
     1       24H    SIGREMO  SECONDARIES//)
 2030 FORMAT(1X,I5,1X,1P6E12.4)
 2040 FORMAT(/6X,14HTOTAL VOLUME =,1PE11.4,22X,15HOUTER SURFACE =,E11.4)
 2050 FORMAT(/101X,17(1H*),12HINPUT ERROR.)
C
C READ RADII, XS-TOTAL AND XS-SCATTERING.
C
      READ(NIN, 1010) (RAD(M),  M = 1, MMAX)
      READ(NIN, 1010) (SIG(M),  M = 1, MMAX)
      READ(NIN, 1010) (SIGS(M), M = 1, MMAX)
C
C CALCULATE VARIOUS DERIVED QUANTITIES.
C
      SWERR = .FALSE.
      VA = 0.0
C
      DO 10 M = 1, MMAX
      R2 = RAD(M)*RAD(M)
      VOL(M) = PI*(R2 - VA)
      VA = R2
      IF(VOL(M).LE.0.0) SWERR = .TRUE.
      SIGR(M) = SIG(M) - SIGS(M)
      SIG(M) = SIG(M) + SMALL
      SIG1(M) = 1.0 / SIG(M)
      SEC(M) = SIGS(M) * SIG1(M)
   10 SIGV(M) = SIG(M) * VOL(M)
C
      SB = 2.0*PI*RAD(MMAX)
      VTOT = 0.5*SB*RAD(MMAX)
C
C PRINT INPUT DATA PLUS SOME DERIVED QUANTITIES.
C
      WRITE(NOT, 2010) MMAX, IG
      WRITE(NOT, 2020)
      DO 20 M = 1, MMAX
   20 WRITE(NOT, 2030) M,RAD(M),VOL(M),SIG(M),SIGS(M),SIGR(M),SEC(M)
      WRITE(NOT, 2040) VTOT, SB
      IF(SWERR) WRITE(NOT,2050)
      IF(SWERR) STOP
C
      RETURN
      END