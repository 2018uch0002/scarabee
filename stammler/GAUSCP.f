CGAUSCP     GAUSCP/COLPROB, BARILOCHE 1979
      SUBROUTINE GAUSCP(MMAX, SEC, ST1, VSI, P, Y, X)
C
C DEDICATED SUBROUTINE TO SOLVE, BY GAUSSIAN ELIMINATION, RESPONSE
C FLUXES TO UNIT IN-CURRENT AND TO UNIT REGION SOURCE DENSITIES,
C FOR A SYSTEM OF MMAX REGIONS. IT SETS UP THE COMMON EQUATION
C MATRIX OF MMAX*MMAX, NEXT SOLVES IT FOR (MMAX+1) RIGHT SIDES.
C
C SEC() = NUMBER OF SECONDARIES.
C ST1() = INVERSE SIGMA TOTAL.
C VSI() = VOLUME*SIGMA.
C P(I, J) = AT FIRST, THE SYMMETRIC COLLISION PROBABILITY MATRIX,
C           DIRECTLY TRANSFORMED INTO THE NON-SYMMETRIC MATRIX OF
C           THE SET OF EQUATIONS.
C Y() = AT FIRST, THE FIRST RHS (PARTIAL BLACKNESSES), LATER
C       ITS THE SOLUTION, I.E., THE FLUXES CAUSED BY ONE NEUTRON
C       ENTERING THE MMAX-REGION SYSTEM.
C X(I, K) = (K+1)TH RHS, NEXT ITS SOLUTION, I.E., THE FLUXES IN
C           THE REGIONS I, CAUSED BY A UNIT SOURCE DENSITY IN K.
C
      DIMENSION SEC(1), ST1(1), VSI(1), P(MMAX,1), Y(1), X(MMAX,1)
C
C RIGHT-HAND SIDES AND MATRIX, USING THE ORIGINAL SYMMETRY.
C
      DO 20 I = 1, MMAX
      DO 10 J = 1, MMAX
      X(I, J) = ST1(J)*P(J, I)
   10 P(J, I) = -SEC(I)*P(J,I)
   20 P(I, I) = P(I, I) + VSI(I)
C
C SWEEPING LOWER DIAGONAL MATRIX CLEAN.
C
      DO 60 I = 1, MMAX
      F = 1.0 / P(I, I)
      Y(I) = F*Y(I)
      DO 30 J = 1, MMAX
   30 X(I, J) = F*X(I, J)
      IF(I.EQ.MMAX) GO TO 70
      I1 = I + 1
      DO 40 J = I1, MMAX
   40 P(I, J) = F*P(I, J)
C
C THE ACTUAL SWEEPING
C
      DO 60 J = I1, MMAX
      Y(J) = Y(J) - Y(I)*P(J, I)
      DO 50 K = 1, MMAX
   50 X(J, K) = X(J, K) - X(I, K)*P(J, I)
      DO 60 K = I1, MMAX
   60 P(J, K) = P(J, K) - P(I, K)*P(J, I)
C
C SOLVE. THE LAST RHS IS ALREADY THE LAST COMPONENT.
C
   70 IMAX = MMAX - 1
      IF(IMAX.EQ.0) GO TO 90
      DO 80 I = 1, IMAX
      J = MMAX - I
      J1 = J + 1
      DO 80 K = J1, MMAX
      Y(J) = Y(J) - Y(K)*P(J, K)
      DO 80 KK = 1, MMAX
   80 X(J, KK) = X(J, KK) - X(K, KK)*P(J, K)
C
   90 RETURN
      END