CCALFLU       CALFLU/COLPROB, BARILOCHE 1979
      SUBROUTINE CALFLU(MMAXO, VOL, SIGR, YI, XIK, QI,
     1                  VOLY, FLUQ, FLUJ, FLUX, REMO)
C
C READS SOURCE, QI(), ALBEDO, INCIDENT CURRENT (JEXT), AND SWMORE.
C   SWMORE = T/F = YES/NO NEXT CASE WITH NEW QI(), ALBEDO AND JEXT.
C
C CALCULATES FLUQ() = FLUX CAUSED BY THE SOURCES,
C            FLUJ() = FLUX CAUSED BY THE INCIDENT CURRENT.
C            FLUX() = TOTAL FLUX = FLUQ() + FLUJ().
C            JPLUS  = PARTIAL OUT-CURRENT.
C            JMINUS = PARTIAL IN-CURRENT.
C            JNET   = NET OUT-CURRENT (JPLUS - JMINUS).
C
      DIMENSION VOL(1), SIGR(1), YI(1), XIK(MMAXO,1), QI(1),
     1          VOLY(1), FLUQ(1), FLUJ(1), FLUX(1), REMO(1)
      COMMON / CPR001 / NIN, NOT, NX1(2)
      COMMON / CPR002 / MMAX, NX202, ALBEDO, NX204, SB, NX206
      LOGICAL SWMORE
      REAL JEXT, JMINUS, JNET, JPLUS
C
 1010 FORMAT(6E12.4)
 1020 FORMAT(2E12.4,L6)
 2010 FORMAT(/40(2H -)//
     1       12X,8HALBEDO =,1PE11.4,31X,6HJEXT =,E11.4/
     2       14X,6HJNET =,1PE11.4,6X,7HJPLUS =,E11.4,
     3        5X,8HJMINUS =,E11.4)
 2020 FORMAT(//43H REGION   SOURCE      SOURFLUX      CURFLUX  ,
     1         22H  TOTAL FLUX   REMOVALS//)
 2030 FORMAT(1X,I5,1X,1P5E12.4)
 2040 FORMAT(//7H RTOT(=,1PE11.4,10H) + JNET(=,E11.4,3H) =,e11.4,
     1         9H = QTOT(=,E11.4,2H).)
C
C READ SOURCE DISTRIBUTION AND IN-CURRENT.
C
   10 READ(NIN, 1010) (QI(I), I = 1, MMAX)
      READ(NIN, 1020) ALBEDO, JEXT, SWMORE
C
C EVALUATE BLACK BOUNDARY SOURCE LEAKAGE, X, AND CURRENTS.
C
      QSB = 0.25*SB
      AQSB = ALBEDO*QSB
      X = 0.0
      Y = 1.0
C
      DO 20 I = 1, MMAX
      VOLY(I) = VOL(I) * YI(I)
      X = X + VOLY(I)*QI(I)
   20 Y = Y - VOLY(I)*SIGR(I)
C
      X = QSB*X
      BFACT = 1.0 / (1.0 - ALBEDO*Y)
      JPLUS = BFACT*(X + Y*JEXT)
      JMINUS = BFACT*(ALBEDO*X + JEXT)
      JNEX = JPLUS - JMINUS
C
C CALCULATE ACTUAL RESPONSE FLUXES.
C
      DO 30 I = 1, MMAX
      YI(I) = BFACT*YI(I)
      FACT = AQSB*YI(I)
      DO 30 K = 1, MMAX
   30 XIK(I, K) = XIK(I, K) + FACT*VOLY(K)
C
C CALCULATE FLUXES AND REMOVALS PER REGIONS.
C
      QTOT = 0.0
      RTOT = 0.0
C
      DO 50 I = 1, MMAX
      SUM = 0.0
      DO 40 K = 1, MMAX
   40 SUM = SUM + QI(K)*XIK(I, K)
      FLUQ(I) = SUM
      FLUJ(I) = JEXT*YI(I)
      FLUX(I) = SUM + FLUJ(I)
      REMO(I) = SIGR(I)*FLUX(I)*VOL(I)
      QTOT = QTOT + QI(I)*VOL(I)
   50 RTOT = RTOT + REMO(I)
C
C PRINTOUT.
C
      WRITE(NOT,2010) ALBEDO, JEXT, JNET, JPLUS, JMINUS
      WRITE(NOT,2020)
      WRITE(NOT,2030) (I,QI(I),FLUQ(I),FLUJ(I),FLUX(I),REMO(I),I=1,MMAX)
      RJTOT = RTOT + JNET
      WRITE(NOT,2040) RTOT, JNET, RJTOT, QTOT
C
      IF(SWMORE) GO TO 10
      RETURN
      END